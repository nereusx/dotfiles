#!/bin/tcsh -f
#
#	Package and service manager for Void Linux
#
#	Copyright (C) 2019, Nicholas Christopoulos <nereus@freemail.gr>
#
#	This program is free software; you can redistribute it and/or modify it
#	under the terms of the GNU General Public License as published by the Free
#	Software Foundation; either version 3 of the License, or (at your option)
#	any later version.
#

###
set todo = help
if ( $#argv > 0 ) then
	set todo = $1
endif

switch ( $todo )
#
#	basic package utilities
#
case -i:
case install:
	xbps-install $argv[2-]
	breaksw
case -r:
case remove:
	xbps-remove $argv[2-]
	breaksw
case reinstall:
	xbps-remove $argv[2-]
	xbps-install -y $argv[2-]
	breaksw
case -u:
case update:
	xbps-install -Su $argv[2-]
	xlocate -S
	breaksw

#
#	advanced package utilities
#
case -v:
case info:
case show:
case view:
	xbps-query -R -S $argv[2-]
	breaksw

case -s:
case search:
	xbps-query -R -s $argv[2-]
	breaksw

case -rs:
case regex-search:
	xbps-query -R -o $argv[2-]
	breaksw

case -f:
case -fs:
case file-search:
	xlocate $argv[2-]
	breaksw

#
#	services
#
case -e:
case -es:
case enable:
case enable-service:
	foreach f ( $argv[2-] )
		if ( -d /etc/sv/$f ) then
			ln -svf /etc/sv/$f /var/service/$f
		else
			echo "ERROR [/etc/sv/$f] not a directory"
		endif
	end
	breaksw

case -d:
case -ds:
case disable:
case disable-service:
	foreach f ( $argv[2-] )
		if ( -e /var/service/$f ) then
			rm /var/service/$f
		else
			echo "ERROR [/var/service/$f] does not exists"
		endif
	end
	breaksw

case start:
case stop:
case status:
case restart:
	foreach f ( $argv[2-] )
		switch ( $f )
		case avahi:
			set service = avahi-daemon
			breaksw
		case nfs:
		case nfsd:
			set service = nfs-server
			breaksw
		case samba:
		case smb:
			set service = smbd
			breaksw
		default:
			set service = $f
		endsw
		sv $todo $service
	end
	breaksw

case -ld:
case -lds:
case -av:
case list-disabled:
case avail:
	set la = ( /etc/sv/* )
	set lb = ( /var/service/* )
	foreach a ( $la )
		if ( ! -l $a ) then
			set found = 0
			foreach b ( $lb )
				if ( $a:t == $b:t ) then
					set found = 1
					break
				endif
			end
		
			if ( $found == 0 ) then
				echo $a:t
			endif
		endif
	end
	breaksw

case -le:
case -les:
case -nav:
case list-enabled:
	set la = ( /etc/sv/* )
	set lb = ( /var/service/* )
	foreach a ( $la )
		if ( ! -l $a ) then
			set found = 0
			foreach b ( $lb )
				if ( $a:t == $b:t ) then
					set found = 1
					break
				endif
			end
			
			if ( $found != 0 ) then
				echo $a:t
			endif
		endif
	end
	breaksw

case -o:
case autoremove:
	xbps-remove -Oo
	breaksw

case -rc:
case reconfigure:
	xbps-reconfigure -f $argv[2-]
	breaksw

case -rok:
case remove-old-kernels:
	vkpurge rm all
	breaksw

case --files:
	xbps-query -f $argv[2-]
	breaksw

help:
default:
	cat <<EOF
Package and service manager for Void Linux, version 1.1

Packages:
    -i  | install ...             install package[s]
    -r  | remove ...              remove package[s]
    -u  | update ...              update package list and upgrade all installed packages
    reinstall ...                 reinstall package[s]
    -s  | search <pattern>        search for packages by matching package-name
    -v  | show | info | view ...  display information about package[s]
    -f  | file-search ...         search file in packages

Services:
    -e[s]  | enable[-service] <service>
    -d[s]  | disable[-service] <service>
    -le[s] | -nav | list-enabled  display all actived services
    -ld[s] | -av | list-disabled  display all available (disabled) services
    start | stop | restart | status <service>

Special:
    -o   | autoremove              remove obsolete packages
    -rc  | reconfigure <package>
    -rok | remove-old-kernels
    --files <package>              shows the files of the package

EOF
endsw

