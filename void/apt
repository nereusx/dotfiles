#!/bin/tcsh -f
#
#	Package and service manager for Void Linux
#
#	Copyright (C) 2019, Nicholas Christopoulos <nereus@freemail.gr>
#
#	This program is free software; you can redistribute it and/or modify it
#	under the terms of the GNU General Public License as published by the Free
#	Software Foundation; either version 3 of the License, or (at your option)
#	any later version.
#

set todo = help
if ( $#argv > 0 ) then
	set todo = $1
endif

switch ( $todo )
#
#	basic package utilities
#
case -i:
case i:
case install:
	xbps-install -y $argv[2-]
	breaksw
case -r:
case r:
case remove:
	xbps-remove $argv[2-]
	breaksw
case reinstall:
	xbps-remove $argv[2-]
	xbps-install -y $argv[2-]
	breaksw
case -u:
case u:
case update:
	xbps-install -Suy $argv[2-]
	breaksw

#
#	advanced package utilities
#
case -v:
case v:
case info:
case show:
case view:
	xbps-query -R -S $argv[2-]
	breaksw

case -s:
case s:
case search:
	xbps-query -R -s $argv[2-]
	breaksw

case -rs:
case rs:
case regex-search:
	xbps-query -R --regex -s $argv[2-]
	breaksw

case -f:
case -fs:
case f:
case fs:
case file-search:
	xbps-query -f $argv[2-]
	breaksw

#
#	services
#
case -e:
case e:
case enable:
	foreach f ( $argv[2-] )
		if ( -d /etc/sv/$f ) then
			ln -svf /etc/sv/$f /var/service/$f
		else
			echo "ERROR [/etc/sv/$f] not a directory"
		endif
	end
	breaksw

case -d:
case d:
case disable:
	foreach f ( $argv[2-] )
		if ( -e /var/service/$f ) then
			rm /var/service/$f
		else
			echo "ERROR [/var/service/$f] does not exists"
		endif
	end
	breaksw

case start:
	sv start $2
	breaksw

case stop:
	sv stop $2
	breaksw

case restart:
	switch ( $2 )
	case avahi:
		set service = avahi-daemon
		breaksw
	case nfs:
	case nfsd:
		set service = nfs-server
		breaksw
	default:
		set service = $2
	endsw
	sv restart $service
	breaksw

case status:
	sv status $2
	breaksw

case -av:
case av:
case avail:
	set la = ( /etc/sv/* )
	set lb = ( /var/service/* )
	foreach a ( $la )
		set found = 0
		foreach b ( $lb )
			if ( $a:t == $b:t ) then
				set found = 1
				break
			endif
		end
		
		if ( $found == 0 ) then
			echo $a:t
		endif
	end
	breaksw

case -nav:
case nav:
case notavail:
	set la = ( /etc/sv/* )
	set lb = ( /var/service/* )
	foreach a ( $la )
		set found = 0
		foreach b ( $lb )
			if ( $a:t == $b:t ) then
				set found = 1
				break
			endif
		end
		
		if ( $found != 0 ) then
			echo $a:t
		endif
	end
	breaksw

help:
default:
#	@ ws = ( `tput cols` / 2 )
	@ ws = 40
	set sp = "\033[${ws}G"
	echo "Package and service manager for Void Linux, version 1.0"
	echo "\nPackages:"
	echo "    -i  | install ... ${sp} install packages"
	echo "    -r  | remove ... ${sp} remove packages"
	echo "    -u  | update ... ${sp} update package list and upgrade all packages"
	echo "    reinstall ... ${sp} reinstall package"
	echo "    -s  | search <pattern> ${sp} search for packages by matching PKG, STRING"
	echo "    -rs | regex-search <pattern>"
	echo "    -v  | show | info | view ... ${sp} display package information"
	echo "\nServices:"
	echo "    -e  | enable  service"
	echo "    -d  | disable service"
	echo "    start | stop | restart | status service"
	echo "    -av  | avail ${sp} available services"
	echo "    -nav | notavail ${sp} not available services"
endsw

