#!/bin/tcsh -f

set todo = help
if ( $#argv > 0 ) then
	set todo = $1
endif
switch ( $todo )
case -i:
case i:
case install:
	xbps-install $argv[2-]
	breaksw
case -r:
case r:
case remove:
	xbps-remove $argv[2-]
	breaksw
case reinstall:
	xbps-remove $argv[2-]
	xbps-install $argv[2-]
	breaksw
case -u:
case u:
case update:
	xbps-install -SAuy $argv[2-]
	breaksw

#
case -v:
case v:
case info:
case show:
case view:
	xbps-query -R -S $argv[2-]
	breaksw

case -s:
case s:
case search:
	xbps-query -R -s $argv[2-]
	breaksw
case -rs:
case rs:
	xbps-query -R --regex -s $argv[2-]
	breaksw
case -f:
case -fs:
case f:
case fs:
case file-search:
	xbps-query -f $argv[2-]
	breaksw

#
#	services
#
case -e:
case enable:
	foreach f ( $argv[2-] )
		if ( -d /etc/sv/$f ) then
			ln -svf /etc/sv/$f /var/service/$f
		else
			echo "ERROR [/etc/sv/$f] not a directory"
		endif
	end
	breaksw

case -d
case disable:
	foreach f ( $argv[2-] )
		if ( -e /var/service/$f ) then
			rm /var/service/$f
		else
			echo "ERROR [/var/service/$f] does not exists"
		endif
	end
	breaksw

case start:
	sv start $2
	breaksw

case stop:
	sv stop $2
	breaksw

case restart:
	switch ( $2 )
	case avahi:
		set service = avahi-daemon
		breaksw
	case nfs:
	case nfsd:
		set service = nfs-server
		breaksw
	default:
		set service = $2
	endsw
	sv restart $service
	breaksw

case status:
	sv status $2
	breaksw

case -av:
case avail:
	set la = ( /etc/sv/* )
	set lb = ( /var/service/* )
	foreach a ( $la )
		set found = 0
		foreach b ( $lb )
			if ( $a:t == $b:t ) then
				set found = 1
				break
			endif
		end
		
		if ( $found == 0 ) then
			echo $a:t
		endif
	end
	breaksw

case -nav:
case notavail:
	set la = ( /etc/sv/* )
	set lb = ( /var/service/* )
	foreach a ( $la )
		set found = 0
		foreach b ( $lb )
			if ( $a:t == $b:t ) then
				set found = 1
				break
			endif
		end
		
		if ( $found != 0 ) then
			echo $a:t
		endif
	end
	breaksw

help:
default:
	echo "--- packages ---"
	echo "-i | install packs"
	echo "-r | remove packs"
	echo "-u | update/upgrade all"
	echo "reinstall packs"
	echo "s | search pattern"
	echo "s | search pattern"
	echo "show | info | view package"
	echo " "
	echo "--- services ---"
	echo "-e | enable  service"
	echo "-d | disable service"
	echo "start | stop | restart | status service"
	echo "-av  | avail                 available services"
	echo "-nav | notavail              not available services"
endsw

