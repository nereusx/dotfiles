#!/bin/tcsh -f
#
# View man page for Xorg
#
# v2.0 2019/12/23, added support for man-page in manpath
#
# Copyright (C) 1998-2019 Nicholas Christopoulos (nereus@freemail.gr)
#
# This program and its data-file is free software: you can redistribute
# it and/or modify it under the terms of the GNU General Public License 
# as published by the Free Software Foundation, either version 3 of
# the License, or any later version.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.
#

# select viewer
set vw = xpdf
set vwlst = ( /usr/bin/epdfview /usr/bin/qpdfview /usr/bin/evince /usr/bin/okular /usr/bin/xpdf )
foreach f ( $vwlst )
	if ( -e $f ) then
		set vw = $f
		break
	endif
end

#
set src = "$1"
if ( $#argv < 1 ) then
	echo "usage: xview-roff file or man-page"
	exit 1
endif

# if itsnt a file but a man page; find the correct file
if ( ! -e $src ) then
	unset found
	set mpath = ( /usr/share/man /usr/local/share/man /usr/man /usr/local/man )
	foreach nd ( 1 2 3 4 5 6 7 8 n 0p 1p )
		foreach e ( $mpath )
			set t = ${e}/man${nd}/${src}.${nd}
			if ( -e $t ) then
				set src = $t
				set found
				break
			endif
			if ( -e $t.gz ) then
				set src = $t.gz
				set found
				break
			endif
		end
		if ( $?found ) break
	end
endif
if ( ! -e $src ) then
	echo "file '$src' not found"
	exit 1
endif
# select cat or zcat
set catcmd = cat
if ( $src:e == "gz" ) then
	set catcmd = zcat
	set src = $src:r
endif

# setup temporary file
set tf = `mktemp -t xmanx-XXXXXXXX`
set nf = ${tf}.pdf
mv $tf $nf

# create pdf
set ext = $src:e
switch ( $ext )
case ms:
	$catcmd $src | groff -Tpdf -m ms > $nf
	breaksw
case me:
	$catcmd $src | groff -Tpdf -m me > $nf
	breaksw
case mm:
	$catcmd $src | groff -Tpdf -m mm > $nf
	breaksw
case mdoc:
	$catcmd $src | groff -Tpdf -m mdoc > $nf
	breaksw
case man:
case [0-1p]:
case [1-9n]:
	$catcmd $src | groff -Tpdf -m man > $nf
	breaksw
default:
	$catcmd $src | groff -Tpdf -m mom > $nf
endsw

# show
$vw $nf

# cleanup
rm $nf
