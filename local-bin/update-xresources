#!/bin/tcsh

# resources directory
set xres = ${HOME}/.xres

# backup directory
if ( ! $?backup ) then
	set backup = ${HOME}
endif

# default colorset
set colorset = default

###
while ( $# )
	switch ( $1 )
	case "-h":
	case "--help":
		echo "update-xresources [ -c colorset ]"
		exit 1
	case "-v":
	case "--version":
		echo "update-xresources version 1.0"
		exit 1
	case "-c":
		if ( $# > 1 ) then
			set colorset = $2
			if ( -e "${xres}/${colorset}.cset" ) then
				echo "$colorset theme selected"
				ln -sf "${xres}/${colorset}.cset" "${xres}/default.cset"
			else
				echo "${xres}/${colorset}.cset file missing"
			endif
		else
			echo "colorset parameter missing"
		endif
	endsw
	shift
end

###

if ( -d ${xres} ) then
	# backup
	if ( -e ~/.Xresources ) then
		mv ~/.Xresources ${backup}/.Xresources.bak
	endif
	
	# set color theme
	if ( -e "${xres}/${colorset}.cset" ) then
		echo "!  === ${colorset} theme: " > ~/.Xresources
		cat ${xres}/${colorset}.cset >> ~/.Xresources
	else
		echo '!  === no color theme' > ~/.Xresources
	endif
	
	# all resources
	foreach f ( ${xres}/*.res )
		if ( -r $f ) then
			echo "!  === ${f}: " >> ~/.Xresources
			cat $f >> ~/.Xresources
		endif
	end
	
	# merge it
	echo -n "${HOME}/.Xresources created"
	if ( $?DISPLAY ) then
		if ( { xrdb -merge ~/.Xresources } ) then
			echo -n " and merged"
		else
			echo -n ", but NOT merged"
		endif
	endif
	echo .
else
	echo "${xres} does not exists"
endif
