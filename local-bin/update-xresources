#!/bin/tcsh -f

# resources directory
set xres=${HOME}/.xres

# backup directory
if ( ! $?backup ) then
	set backup=${HOME}
endif

# default colorset
set colorset=linux

###

if ( -d ${xres} ) then
	# backup
	if ( -e ~/.Xresources ) then
		mv ~/.Xresources ${backup}/.Xresources.bak
	endif
	
	# set color theme
	if ( -e ${xres}/${colorset}.cset ) then
		echo "!  === ${colorset} theme: " > ~/.Xresources
		cat ${xres}/${colorset}.cset >> ~/.Xresources
	else
		echo '!  === no color theme' > ~/.Xresources
	endif
	
	# all resources
	foreach f ( ${xres}/*.res )
		echo "!  === ${f}: " >> ~/.Xresources
		cat $f >> ~/.Xresources
	end
	
	# merge it
	echo -n "${HOME}/.Xresources created"
	if ( $?DISPLAY ) then
		if ( { xrdb -merge ~/.Xresources } ) then
			echo -n " and merged"
		else
			echo -n ", but NOT merged"
		endif
	endif
	echo .
else
	echo "${xres} does not exists"
endif
