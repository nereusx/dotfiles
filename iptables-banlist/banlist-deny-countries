#!/bin/tcsh -f

# LC = local
set ALLOWED = ( LC GR CY )
set IPRCNT  = /proc/net/xt_recent

#
set IPLIST = `cat $IPRCNT/CHECKIP | awk '/src=/ { print substr($1,5) }'`
foreach IP ( $IPLIST )
	set geoinfo = `geoiplookup "$IP" | head -n 1 | awk -F ": " '{print$2}'`
	echo $geoinfo | grep -i "IP Address not found" > /dev/null
	if ( $status == 0 ) then
		set COUNTRY = "LC"
	else
		set COUNTRY = `echo "$geoinfo" | awk -F "," '{ print $1 }'`
	endif
	unset found
	foreach CC ( $ALLOWED )
		if ( $CC == $COUNTRY ) then
			set found
			break
		endif
	end
	if ( $?found ) then
		unset found
	else
		logger -t "BANLIST" "Add $IP $geoinfo"
		echo "+$IP" > $IPRCNT/BANLIST
		echo "-$IP" > $IPRCNT/CHECKIP
	endif
end



